# $Id: PKGBUILD 174838 2013-01-07 19:57:53Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=hdf5-cpp-fortran-mpich2
_pkgname=hdf5
_prefix=/opt/mpich2
_mpirun=${_prefix}/bin/mpiexec
pkgver=1.8.12
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="General purpose library and file format for storing scientific data"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh' 'mpich2' 'gcc-libs')
makedepends=('time' 'gcc-fortran')
#conflicts=('hdf5')
#provides=('hdf5')
groups=('modified')
source=(ftp://ftp.hdfgroup.org/HDF5/releases/${_pkgname}-${pkgver/_/-}/src/${_pkgname}-${pkgver/_/-}.tar.bz2)
options=('!libtool' 'staticlibs')
md5sums=('03ad766d225f5e872eb3e5ce95524a08')

build() {
  cd $srcdir/${_pkgname}-${pkgver/_/-}
  unset CFLAGS
  unset CXXFLAGS
  ./configure \
         FC=${_prefix}/bin/mpif90 \
         F9X=${_prefix}/bin/mpif90 FFLAGS="-I${_prefix}/include" \
         CC=${_prefix}/bin/mpicc CXX=${_prefix}/bin/mpicxx \
         RUNSERIAL="${_mpirun} -n 1" RUNPARALLEL="${_mpirun} -n $$NPROCS" \
          --prefix=${_prefix} \
          --enable-parallel \
          --enable-hl \
          --enable-linux-lfs \
          --enable-production \
          --enable-fortran \
          --enable-fortran2003 \
          --enable-shared \
          --with-pic \
          --docdir=${_prefix}/share/${_pkgname} \
          --with-pthread=/usr/lib
  make
}

package() {
  cd $srcdir/${_pkgname}-${pkgver/_/-}

  make -j1 DESTDIR=${pkgdir} install

  install -d -m755 $pkgdir/usr/share/licenses/${pkgname}
  install -m644 $srcdir/${_pkgname}-${pkgver/_/-}/COPYING \
          $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}

