# $Id: PKGBUILD 174838 2013-01-07 19:57:53Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=hdf5-cpp-fortran
_pkgname=hdf5
pkgver=1.8.10_patch1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="General purpose library and file format for storing scientific data"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh' 'gcc-libs')
makedepends=('time' 'gcc-fortran')
conflicts=('hdf5')
provides=('hdf5')
groups=('modified')
source=(ftp://ftp.hdfgroup.org/HDF5/prev-releases/${_pkgname}-${pkgver/_/-}/src/${_pkgname}-${pkgver/_/-}.tar.bz2)
options=('!libtool')
sha1sums=('458cb91496e313debd55d52a7f89459a5469cceb')

build() {
  cd $srcdir/${_pkgname}-${pkgver/_/-}
  export CFLAGS="${CFLAGS/O2/O0}"
  export CXXFLAGS="${CFLAGS}"
  export FCFLAGS="${CFLAGS}"
  ./configure --prefix=/usr \
    --enable-hl \
    --enable-cxx \
    --enable-fortran \
    --enable-fortran2003 \
    --enable-linux-lfs \
    --enable-production \
    --with-pic \
    --docdir=/usr/share/doc/hdf5/ \
    --with-pthread=/usr/lib/ \
    --disable-sharedlib-rpath
  make
}

package() {
  cd $srcdir/${_pkgname}-${pkgver/_/-}

  make -j1 DESTDIR=${pkgdir} install

  install -d -m755 $pkgdir/usr/share/licenses/${pkgname}
  install -m644 $srcdir/${_pkgname}-${pkgver/_/-}/COPYING \
          $pkgdir/usr/share/licenses/${pkgname}/LICENSE 
}

