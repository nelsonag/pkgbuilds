# Maintainer: Rich Li <rich@dranek.com>
pkgname=python-h5py-openmpi
pkgver=2.3.0
pkgrel=1
pkgdesc="General-purpose Python bindings for the HDF5 library using OpenMPI"
url="http://www.h5py.org"
arch=('i686' 'x86_64')
license=('BSD')
depends=('hdf5-openmpi' 'python-mpi4py' 'python-numpy')
makedepends=('cython')
conflicts=('python-h5py')
provides=('python-h5py')
source=(https://github.com/h5py/h5py/archive/"$pkgver".tar.gz)
md5sums=('2dca39fb2ff6e70824487201e1d1fb84')
sha1sums=('10540a8cd2de9b12e9c08da5a2c70ad22af948fe')
sha256sums=('a2c60ed924195714c3ac3e2816a9c47bf092b43d9e519ccb18faa71ec00d8680')

 
build() {
    cd "$srcdir/h5py-$pkgver"
    export CC=mpicc
    python setup.py build --mpi
}

check() {
    cd "$srcdir/h5py-$pkgver"
    export CC=mpicc
    python setup.py test --mpi
}
 
package() {
    cd "$srcdir/h5py-$pkgver"
    python setup.py install --mpi --prefix=/usr --root="$pkgdir/" --optimize=1
    install -D -m644 licenses/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
